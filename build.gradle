group 'com.hyperparticle'
version '0.1.4'

buildscript {
    ext.kotlin_version = '1.0.6'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.13"
    }
}

apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'LibrarianKt'

defaultTasks 'run'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile group: 'io.kotlintest', name: 'kotlintest', version: '1.3.5'
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.6'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

dokka {
    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/Hyperparticle/freeze-tag/tree/master/src/main/kotlin"
        suffix = "#L"
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/dokkaJavadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'bsu.slim'
            artifactId 'freeze-tag'
            version '0.1.4'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'freeze-tag'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Hyperparticle/freeze-tag.git'
        version {
            name = '0.1.4'
        }
    }
}
